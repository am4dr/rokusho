
plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51' apply false
}

ext {
    packageProjects = ['app', 'javafx-commons', 'gui', 'core', 'app-gui'].collect { project ":$it" }
    libraries = [
            kotlin: [
                    stdlib: 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51',
                    reflect: 'org.jetbrains.kotlin:kotlin-reflect:1.2.51',
            ],
            junit: [
                    api: ['org.junit.jupiter:junit-jupiter-api:5.2.0',
                          'org.junit.jupiter:junit-jupiter-params:5.2.0'],
                    engine: 'org.junit.jupiter:junit-jupiter-engine:5.2.0',
            ],
            commons: [
                    cli: 'commons-cli:commons-cli:1.4',
            ],
            snakeyaml: 'org.yaml:snakeyaml:1.21',
            slf4j: [
                    api: 'org.slf4j:slf4j-api:1.7.25',
            ],
            logback: 'ch.qos.logback:logback-classic:1.2.3',
            sample_viewer: 'com.github.am4dr.javafx:gui-sample-viewer:0.4.2-SNAPSHOT',
    ]
}

allprojects {
    group = 'com.github.am4dr.rokusho'
    version = '0.2.0-SNAPSHOT'
    repositories {
        mavenLocal()
        jcenter()
    }
}

project(':application') {
    apply plugin: 'application'
    archivesBaseName = "rokusho-$it.name"

    mainClassName = 'com.github.am4dr.rokusho.launcher.Launcher'
    dependencies {
        runtimeOnly packageProjects
    }
    jar {
        onlyIf { false }
    }
    run {
        if (project.hasProperty('args')) {
            args = project.getProperty('args').split(/\s+/).grep()
        }
    }
}

configure(packageProjects) {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'java-library'
    archivesBaseName = "rokusho-$it.name"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = '1.8'
    }
    dependencies {
        api libraries.kotlin.stdlib

        testImplementation libraries.junit.api
        testRuntimeOnly libraries.junit.engine

        api libraries.slf4j.api
        runtimeOnly libraries.logback
    }
    test {
        useJUnitPlatform()
    }
}
project(':app-gui') {
    dependencies {
        implementation project(':app')
        implementation project(':core')
        implementation project(':javafx-commons')
        implementation project(':gui')
        implementation libraries.commons.cli
    }
}
project(':gui') {
    sourceSets {
        sample
    }
    dependencies {
        implementation project(':javafx-commons')

        sampleImplementation libraries.kotlin.reflect
        sampleImplementation libraries.sample_viewer
        sampleImplementation sourceSets.main.runtimeClasspath
    }
    task runGuiSamples(type: JavaExec, dependsOn: classes) {
        classpath sourceSets.sample.runtimeClasspath
        main = 'com.github.am4dr.rokusho.gui.sample.GUISamples'
        args = ['--path=build/classes/kotlin/main',
                '--load-only-path=build/classes/kotlin/sample']
    }
    task runGuiSamplesAllReload(type: JavaExec, dependsOn: classes) {
        classpath sourceSets.sample.runtimeClasspath
        main = 'com.github.am4dr.rokusho.gui.sample.GUISamples'
        args = ["--path=${['build/classes/kotlin/main', 'build/classes/kotlin/sample'].join(File.pathSeparator)}",
        ]
    }
}
project(':core') {
    dependencies {
        implementation project(':javafx-commons')
    }
}
project(':app') {
    dependencies {
        api project(':core')
        implementation project(':javafx-commons')
        implementation libraries.snakeyaml
    }
}
